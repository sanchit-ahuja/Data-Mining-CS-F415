var source_file_path
set source_file_path = "C:\Users\FDUSER.M-1845.002\Desktop\Lab11"
var source_file_name
set source_file_name = "\s1_modified.csv"
var lower_limit
var upper_limit
set lower_limit = 2
set upper_limit = 5
var kname
var dname
var t
var temp
var d
var gname
var g
var vname
var ymid
set ymid = 150 * (^upper_limit/2)
create stream "Kmeans Analysis using Scripting" #Created a new stream
set vname = create variablefilenode
set ^vname.full_filename = ^source_file_path>< ^source_file_name
set ^vname.read_field_names = true
set ^vname.delimit_space = true
set ^vname.multi_blank = true
set ^vname.default_value_mode = Read
for i from ^lower_limit to ^upper_limit
    set temp = 150 * (^i-1)
    set kname = create kmeansnode #Created KmeansNode
    set t = "Kmeans " >< ^i
    set ^kname.model_name = ^t
    connect ^vname to ^kname #Connecting variableNode to KmeansNode
    set ^kname.num_clusters = ^i
    set ^kname.gen_distance = true
    execute ^kname
    set dname = create derivenode #Creating a derivenode to get mse from KMeansNugget
    position ^dname at 450 ^temp
    connect ^t:applykmeansnode to ^dname #Connecting KmeansNugget to derivedNode
    set d = "Square Error " ><^i
    set ^dname.new_name = ^d
    set ^dname.result_type = Formula
    set ^dname.formula_expr = "'$KMD-" >< ^t ><"'*'$KMD-">< ^t ><"'"
    set gname = create setglobalsnode
    position ^gname at 550 ^temp
    set ^gname.custom_name = ^d
    connect ^dname to ^gname
    set ^d:setglobalsnode.globals.^d = [Sum]
    set ^d:setglobalsnode.clear_first = False
    execute ^dname
endfor

create reportnode #Used to create csv file to
connect ^vname to :reportnode #Connecting the source node to reportnode
set :reportnode.output_mode = File
set :reportnode.output_format = Text
set :reportnode.full_filename = ^source_file_path>< "\kmeans-output.txt"
set :reportnode.text = ""
for i from ^lower_limit to ^upper_limit
    set :reportnode.text = :reportnode.text>< ^i><", [@GLOBAL_SUM('Square Error " >< ^i>< "')]\n" #Adding all the Squared Errors
endfor
execute :reportnode


set vname = create variablefilenode
position ^vname at 650 ^ymid
set ^vname.full_filename = ^source_file_path>< "\kmeans-output.txt"
set ^vname.read_field_names = false
set ^vname.delimit_comma = true
set ^vname.multi_blank = true
set ^vname.default_value_mode = Read
set ^vname.type.field1 = Range
set ^vname.type.field2 = Range
set ^vname.new_name.field1 = "No of Clusters"
set ^vname.new_name.field2 = "Sum of Squared Errors"
create plotnode
position :plotnode at 750 ^ymid
connect ^vname to :plotnode
set :plotnode.x_field = "No of Clusters"
set :plotnode.y_field = "Sum of Squared Errors"
set :plotnode.title = "Plot of SSE Vs. K"
execute :plotnode